#! /usr/bin/env perl

use lib 'lib';
use YAML qw(LoadFile);
use Try::Tiny;
use Getopt::Long;
use Pod::Usage;
use EV;
use AnyEvent::MQTT;
use AnyEvent;
use AnyEvent::Log;
use DBI;
use POSIX qw(strftime);
use Machine::DB::Responder;
use strict;
use warnings;
use v5.10;

our $VERSION = '0.01';

# Processing of command line options
my $id;
my $config_file = '';
my $verbose;
my $debug;
my $help;

Getopt::Long::Configure('bundling');
GetOptions(
    'i|id:s'     => \$id,
    'c|config=s' => \$config_file,
    'v|verbose'  => \$verbose,
    'd|debug'    => \$debug,
    'V|version'  => sub { die "Vesion: $VERSION\n"; },
    'help|?'     => \$help,
) or pod2usage(-verbose => 0);

$AnyEvent::Log::FILTER->level('info')  if $verbose;
$AnyEvent::Log::FILTER->level('debug') if $debug;

pod2usage(-verbose => 2) if $help;

AE::log 'info'  => "Starting";
AE::log 'debug' => "Working directory: " . `pwd`;

my $mqtt;
my $dbh;
my $commit_timer;
my $config;
my @condvars;

# Check for config file
unless (defined $config_file && -r $config_file) {
    pod2usage(
        -verbose => 1,
        -exitval => "NOEXIT");
    AE::log fatal => "A configuration file is required";
}

# Load the configuration file and start working
reload_config();

my $cv = AnyEvent->condvar;

# Falta agregar tratamiento de senales (stop, restart)
my $w1 = AnyEvent->signal(
    signal => 'TERM',
    cb     => sub { $cv->send }
);

my $w2 = AnyEvent->signal(
    signal => 'HUP',
    cb     => sub {
        AE::log info => "Reloading configuration file";
        reload_config();
    }
);

# Enter the async loop. Disconnect from the database before exiting.
AE::log 'info' => "Entering asynchronous loop";
$cv->recv;

AE::log 'info' => "Disconnecting from the database";
$dbh->disconnect;

my $s = $mqtt->publish(
    topic   => "admin/disconnected/$id",
    message => '',
    retain  => 1,
);
$s->recv;

$mqtt->cleanup;
AE::log 'info' => "Exiting";
exit 0;

sub reload_config {
    # Clear
    $mqtt->cleanup if defined $mqtt && ref $mqtt;
    undef $config;
    $dbh->disconnect if defined $dbh && ref $dbh;
    undef $commit_timer;
    @condvars = ();

    # Read the config file
    try {
        AE::log 'info' => "Loading configuration file from <$config_file>";
        $config = LoadFile($config_file);
    }
    catch {
        pod2usage(
            -exitval  => "NOEXIT",
            -verbose  => 99,
            -sections => "CONFIGURATION FILE",
        );
        AE::log fatal => "Failed parsing the configuration file: $_";
    };

    # Set id
    $id //= $config->{Broker}{client_id} || 'mqtt2db';

    # Connect to the MQTT broker
    try {
        my $c = $config->{Broker};
        AE::log(
            fatal => "Broker section is missing from the configuration file"
        ) unless defined $c;

        $mqtt = AnyEvent::MQTT->new(
            timeout      => 10,
            will_topic   => "admin/disconnected/$id",
            will_message => '',
            will_retain  => 1,
            %$c,
            on_error => sub {
                my ($fatal, $msg) = @_;
                AE::log fatal => $msg if $fatal;
                AE::log error => $msg;
            }
        );
        AE::log 'info' => 'Connecting to broker at <' . $c->{host} .'>';
    }
    catch {
        pod2usage(
            -exitval  => "NOEXIT",
            -verbose  => 99,
            -sections => "CONFIGURATION FILE/MQTT broker",
        );
        AE::log fatal => "Connection to the MQTT broker failed: $_";
    };

    # Connect to the database
    try {
        my $c      = $config->{Database};
        my $drv    = $c->{driver}     || die "Database driver is missing";
        my $dbname = $c->{DBname}     || die "DBname is missing";
        my $acomm  = $c->{autoCommit} // 1;
        AE::log 'info' => "Connecting to <$drv> database <$dbname>";

        $dbh = DBI->connect(
            "dbi:$drv:dbname=$dbname",
            $c->{user},
            $c->{password},
            { AutoCommit => $acomm, RaiseError => 1 })
            || die  "Could not connect to the database: $DBI::errstr";
    }
    catch {
        pod2usage(
            -exitval  => "NOEXIT",
            -verbose  => 99,
            -sections => "CONFIGURATION FILE/Database",
        );
        AE::log fatal => "Connection to the database failed: $_";
    };

    # Process list of response objects
    my %responses;
    foreach my $resp (@{$config->{Responses}}) {
        my ($nam) = keys   %$resp;
        my ($val) = values %$resp;
        
        AE::log 'info' => "Setting up response <$nam>";

        try {
            # Create response objects
            $val->{name} = $nam;
            my $r = Machine::DB::Responder->new(%$val);
            $responses{$nam} = $r;
        }
        catch {
            AE::log fatal => "Unable to create response <$nam>: $_\n";
        };
    }

    # Process list of topics
    foreach my $conv (@{$config->{Topics}}) {
        my ($nam) = keys   %$conv;
        my ($def) = values %$conv;

        AE::log 'info' => "Setting up conversation <$nam>";

        try {
            # Load the class that should process the current topic
            my $class;
            if ($def->{'handler'}) {
                $class = "Machine::DB::Handler::" . $def->{'handler'};
            }
            else {
                $class = 'Machine::DB::Handler';
            }

            AE::log 'debug' => "Loading $class for conversation <$nam>";
            eval "require $class";
            AE::log 'fatal' =>  "Error loading class <$class>: $@"
                if $@;

            AE::log 'debug' => "Instantiating handler for <$nam>";
            $def->{'topic name'} = $nam;

            # Create or load the response object
            if (exists $def->{response} && ref $def->{response}) {
                # Create a new response object
                AE::log debug => "Creating response for <$nam>";
                my $r = Machine::DB::Responder->new($def->{response});
                AE::log fatal => "Failed creating the response for $nam"
                    unless $r;
                $def->{response} = $r;
            }
            elsif (exists $def->{response}) {
                # Fetch the response object
                AE::log fatal => "Response <" . $def->{response}
                    . "> does not exist"
                    unless exists $responses{$def->{response}};
                $def->{response} = $responses{$def->{response}};
            }
            my $handler = $class->new($def);

            AE::log 'info'
                =>  "Subscribing to conversation <$nam> with topic <"
                    . $handler->subscription_topic . ">";
            my $s = $mqtt->subscribe(
                topic    => $handler->subscription_topic,
                callback => $handler->subscription_callback($dbh, $mqtt)
            );
            $s->cb( sub {
                AE::log info => "Subscribed to <$nam>";
            });
            push @condvars, $s;
        }
        catch {
            AE::log fatal => "Failed setting up conversation <$nam>: $_";
        };
    }

    # If the database is not in auto-commit mode, commit every
    if (!$config->{Database}{autoCommit}) {
        my $period = $config->{Database}{'commit every'} || 60;
        AE::log info => "Database changes will commit every $period seconds";
        $commit_timer = AnyEvent->timer(
            after    => $period,
            interval => $period,
            cb       => sub {
                try {
                    $dbh->commit;
                }
                catch {
                    AE::log fatal =>
                        "Failed commiting changes to the database: $_";
                };
            }
        );
    }

    push @condvars, $mqtt->publish(
        topic   => "admin/connected/$id",
        message => ''
    );
}

=pod

=head1 NAME

mqtt2bd - Simple database interaction through MQTT messages

=head1 SYNOPSIS

 mqtt2db [options] --config config_file.yml

 Options:

 i, id          Client identification string
 c, config      Configuration file
 v, verbose     Turn on informational messages
 d, debug       Turn on debugging messages
 V, version     Show program version and exit
 ?, help        Show the documentation and exit

 See the documentation for the description of the configuration file.

=head1 DESCRIPTION

This program implements a MQTT-based interface to a database. In order to work, it subscribes to a list of MQTT topics defined in its configuration file. For each topic, you may define one or more SQL statements to be executed on the database and possibly, the definition of a response MQTT message.

The program parses incoming messages (both the topic and the body of the message) looking for the value of pre-defined fields. These fields are then stored in a hash. SQL statements take their parameters from this hash of fields and they put their results back in the hash. Finally, if there is a response, it may take fields from the hash and send these field values back in the message topic or body.

=head1 CONFIGURATION FILE

The configuration file is in YAML. There is an example configuration file in this distribution. You can find the YAML specification here: L<http://yaml.org/spec/1.0/>.

It requires the following top-level sections:

=head2 MQTT broker

This section defines the connection parameters for the MQTT broker. These come from L<AnyEvent::MQTT>; the contents of this section are passed as-is to the constructor. The most common are:

=over

=item * host - IP address of the broker. It is 'localhost' by default.

=item * port - Port the broker is listening to. Defaults to 1883.

=item * timeout - Timeout for response from the server.

=item * user_name - User name for the broker.

=item * password - Password for the broker.

=item * will_topic, will_message, will_qos, will_retain - Define the message that is sent if this program disconnects abruptly from the
broker.

=back

The example configuration file includes:

 Broker:
    host: localhost
    will_topic: mqtt2db/disconnected
    will_message: "Program mqtt2db is disconnected from broker"
    client_id: mqtt2db

=head2 Database

This section defines the database connection parameters using a mapping of scalars:

 Database:
    Driver: SQLite
    DBname: example.db
    User: ~
    Password: ~

These four options are required.

=head2 Topics

Topics are the most complex part of the configuration file. They are given as a list of hashes to improve readability. A sample definition might look like this:

 Topics:
    - Machine requests for parameters:
        topic: ":area/:machine_type/:machine_id/reference/get"
        SQL: >
            SELECT next_reference
            FROM   machine_state
            WHERE  machine_id = ?
        place holders:
            - machine_id
        response:
            topic: :area/:machine_type/:machine_id/reference/set
            fields:
                - next_reference

Each topic has a name (which is not used by the program), a topic, one or more SQL parameters, a list of place holders for the SQL parameters, and possibly, a response object. The functionality behind topics is hidden in the module Machine::DB::Handler.

=head3 Topic

Each topic in the configuration file needs a MQTT topic to subscribe to. This topic may include fields that need to be parsed in order to interact with the data base. These fields must be prepended with ':'. In the example above, we have the fields I<area>, I<machine_type>, and I<machine_id>. The resulting MQTT topic would be I<+/+/+/reference/get>.

The fields thus parsed are put into a hash reference, which will then be merged with the contents of the message body.

=head3 SQL Statements

There are two ways of defining the SQL messages and their place holders that process incoming MQTT messages. If there is just one SQL statement, it can be defined with the SQL key along with its place holders:

 topic: ":area/:machine_type/:machine_id/reference/get"
 SQL: >
    SELECT next_reference
    FROM   machine_state
    WHERE  machine_id = ?
 place holders:
    - machine_id

On the other hand, if there are multiple statements, they have to be treated differently:

 topic: ":area/:machine_type/:machine_id/alarm/set"
 SQL statements:
    - First statement description:
        SQL: >
            UPDATE machine_state
            SET    alarm = ?
            WHERE  machine_id = ?
        place holders:
            - machine_id
            - alarm
    - Second statement description:
        SQL: >
            SELECT next_reference
            FROM   machine_state
            WHERE  machine_id = ?
        place holders:
            - machine_id

In the example above, the I<alarm> field would come from the body of the MQTT message. SQL statements would be executed in the given order and their results (in the case of SELECT statements) would be put in the hash of fields.

Please note that this program is able to retrieve only a single row of data from the database.

=head3 Response Objects

Optionally, the program may issue a response in the form of an MQTT message. Response objects are defined like this:

 response:
    topic: "machine/:machine_id/reference/set"
    fields:
        - part_number

They only require a topic and a list of fields. The topic may contain fields, prepended with ':', whose values will be taken from the fields hash.

=head3 Imploding Fields

Sometimes, it is useful to maintain a set of values into a single database field. This program can serialize several distinct values into a hash, using JSON, and store this hash in a destination field.

The configuration file would look like this:

 implode all but:
    fields:
        - list
        - of_fields
        - not_to_implode
    destination: table_column_name

This format limits imploded columns to one per topic.

Any field not present in the list will be imploded into the table column defined as destination.

Implosion is done on I<incoming> MQTT messages.

=head3 Exploding Fields

Fields that have been imploded on arrival need to be exploded when going out. A list of fields to explode may be given in the response definition:

 topic: ":area/:type/:machine_id/parameters/get"
 SQL: >
    SELECT current_params
    FROM machine_state
    WHERE machine_id = ?
 place holders:
    - machine_id
 response:
    topic: ":area/:type/:machine_id/parameters/set"
     explode:
        - current_params
    fields:
        - stops_position
        - brush_boolean
        - temp_doser1_sp
        - temp_doser2_sp
        - filling_height
        - filling_bilayer_height

The above example fetches the single field current_params from the database, and then it explodes it and sends back some of the resulting fields to the client.

Exploded database fields need to be serialized using JSON.

=head1 LICENSE

=cut
